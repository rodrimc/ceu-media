#include "c.ceu"
#include "media/media.ceu"

var int width = 640;
var int height = 480;

var Properties.Text one = val Properties.Text (
                          [].."one",
                          [].."sans 40",
                          Region(0, 400, width, 80, 2),
                          0xffff0048,
                        );

var Properties.Text two = val Properties.Text (
                          [].."two",
                          [].."sans 40",
                          Region(0, 400, width, 80, 2),
                          0xffff0048,
                        );

var Properties.Video prop = val Properties.Video (
                          Region(0, 0, width, height, 1),
                          1.0,
                          1.0,
                        );

var[] byte uri = [] .."resources/night.avi";
var[] byte dummy = _;

var&? Scene scene = spawn Scene(Size(width,height));
watching (scene), 12s
do
  await scene.handle.ok;
  par/or do
    var bool flag = true;
    loop do
      var& Properties text;
      if flag then
        text = &one;
      else
        text = &two;
      end
      watching 1s do
        await Play (&dummy, &text, &scene.handle);
      end
      flag = not flag;
    end
  with
    loop do
      watching 2s do
        await Play(&uri, &prop, &scene.handle);
      end
    end
  with
    var _char_ptr_ext k;
    (_,k,_) = await CM_SCENE_KEY  until (_strcmp(k,"q") == 0);
  end
end

escape 0;
