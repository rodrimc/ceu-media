#include "c.ceu"
#include "media/media.ceu"

var int width = 640;
var int height = 480;

var Media.Text one = val Media.Text (
                          [].."one",
                          [].."sans 40",
                          Region(0, 400, width, 80, 2),
                          0xffff0048,
                        );

var Media.Text two = val Media.Text (
                          [].."two",
                          [].."sans 40",
                          Region(0, 400, width, 80, 2),
                          0xffff0048,
                        );

var Media.Video video = val Media.Video (
                          [].."resources/night.avi",
                          Region(0, 0, width, height, 1),
                          1.0,
                          1.0,
                        );

var&? Scene scene = spawn Scene(Size(width,height));
watching (scene), 12s
do
  par/or do
    var bool flag = true;
    loop do
      var& Media text;
      if flag then
        text = &one;
      else
        text = &two;
      end
      watching 1s do
        await Play (&scene.scene, &text);
      end
      flag = not flag;
    end
  with
    loop do
      watching 2s do
        await Play(&scene.scene, &video);
      end
    end
  with
    var _char_ptr_ext k;
    (_,k,_) = await CM_SCENE_KEY  until (_strcmp(k,"q") == 0);
  end
end

escape 0;
