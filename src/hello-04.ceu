#include "c.ceu"
#include "media/media.ceu"

var int width = 640;
var int height = 480;

var Media.Video back = val Media.Video (
                            [].."resources/animGar.mp4",
                            Region(0, 0, width, height, 1),
                            1.0,
                            1.0,
                          );

var Media.Video video = val Media.Video (
                            [].."resources/diode.mp4",
                            Region(0, 0, 320, 240, 2),
                            0.75,
                            1.0,
                          );

var Media.Audio audio = val Media.Audio (
                            [].."resources/choro.mp4",
                            0.5,
                          );

var&? Scene scene = spawn Scene(Size(width,height));
watching (scene) do
  spawn Play(&scene.scene, &back);

  var&? Play play_video = spawn Play(&scene.scene, &video);
  watching (play_video) do
    par/or do
      var _char_ptr_ext key;
      (_,key,_) = await CM_SCENE_KEY until (_strcmp(key,"q") == 0);
    with
      loop do
        var int x = call Player_Get_Int(&play_video.player, "x");
        var int y = call Player_Get_Int(&play_video.player, "y");

        var _char_ptr_ext key;
        (_,key, _) = await CM_SCENE_KEY;
        if _strcmp (key, "Left") == 0 or _strcmp (key, "h") == 0 then
          call Player_Set_Int(&play_video.player, "x", x-10);
        else/if _strcmp (key, "Right") == 0 or _strcmp (key, "l") == 0 then
          call Player_Set_Int(&play_video.player, "x", x+10);
        else/if _strcmp (key, "Up") == 0 or _strcmp (key, "k") == 0 then
          call Player_Set_Int(&play_video.player, "y", y-10);
        else/if _strcmp (key, "Down") == 0 or _strcmp (key, "j") == 0 then
          call Player_Set_Int(&play_video.player, "y", y+10);
        end
      end
    with
      loop do
        var _char_ptr_ext key;
        var bool pressed;
        (_, key, pressed) = await CM_SCENE_KEY
                       until pressed and _strcmp(key,"space")==0;
        spawn Play(&scene.scene, &audio);
        (_, key, pressed) = await CM_SCENE_KEY
                     until pressed and _strcmp (key,"space")==0;
      end
    end
  end
end

escape 0;
